GAL22V10   ; first line : used GAL
CPUXXCMI G1;

;1  2    3   4   5   6   7   8    9  10  11    12
RW  E  A15 A14 A13 A12 A11  A10  VMA SYS BLK   GND
G21 BA DBEN U9E BS  A9  OVMA G22 IACK U8E DBDIR VCC
;13  14  15  16  17  18   19  20  21 22  23    24

; need Input for first two cycles
; use SYS. SYS is Low for two cyc. then high
; need also A9 - use SEL

;EQUATIONS

!OVMA = VMA

; RAM
!U9E = E & !A15                          ; 0-7FFF
     # E & !A14                          ; 8000-BFFF
     # E & !A13                          ; C000-DFFF
     # E & A15 & A14 & A13 & !A12 & !A11 ; E000-E7FF
     # E & A15 & A14 & A13 & A12 & A11 & A10 & A9 & SYS ; FE00-FFFF

; ROM
!U8E = RW & E & VMA & A15 & A14 & A13 & !A12 & A11 & !A10 ; E800-EBFF
     # RW & E & VMA & A15 & A14 & A13 & A12 & !A11        ; F000-F7FF
     # RW & E & VMA & A15 & A14 & A13 & A12 & A11 & !A10  ; F800-FBFF
     # RW & E & VMA & G21                                 ; FCF8-FCFF PROM & 6820
     # RW & E & VMA & A15 & A14 & A13 & A12 & A11 & A10 & A9 & !SYS ; FE00-FFFF only 2 cycles

; IO Output to G2
G22 = VMA & A15 & A14 & A13 & A12 & A11 & A10 ; FC00-FFFF

DBDIR = RW & A15 & A14 & A13 & !A12 & A11 & A10 ; EC00-EFFF

!DBEN = !BA & A15 & A14 & A13 & !A12 & A11 & A10 ; EC00-EFFF

!IACK = RW & E & VMA & A15 & A14 & A13 & A12 & A11 & A10 ; FC00-FFFF Read

DESCRIPTION
Exorciser Decoder G1 v1.0